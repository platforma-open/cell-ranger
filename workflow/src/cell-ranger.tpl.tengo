self := import("@platforma-sdk/workflow-tengo:tpl")

json := import("json")
path := import("@platforma-sdk/workflow-tengo:path")

ll := import("@platforma-sdk/workflow-tengo:ll")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
exec := import("@platforma-sdk/workflow-tengo:exec")
file := import("@platforma-sdk/workflow-tengo:file")
assets := import("@platforma-sdk/workflow-tengo:assets")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
pConstants := import("@platforma-sdk/workflow-tengo:pframes.constants")

genomes := import(":genomes")
pfCountsConv := import(":pf-counts-conv")


self.defineOutputs(
	"cellRangerLog",
	"cellRangerReport",
	"alignedBAM",
	"countMatrixImport"
	)


self.body(func(inputs) {
	inputData := inputs[pConstants.VALUE_FIELD_NAME]
	blockId := inputs.blockId
	species := inputs.species
	fileExtension := inputs.fileExtension

	genomeAssets := genomes.getGenomeAsset(species)

	cellRanger := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.software-cellranger:cellranger")).
		// cmd("find").arg(".").
		// mkDir("genomeIndex").
		// mkDir("fastqs").
		addAsset(genomeAssets, "genomeIndex").
		arg("count").
		// arg("--id=").arg(sampleName).
		arg("--transcriptome=genomeIndex").
		arg("--fastqs=fastqs")
	
	filesByRIndex := {}
	for sKey, inputFile in inputData.inputs() {
		key := json.decode(sKey)
		r := key[0]
		if (r[0] != 'R' && r[0] != "I") || (r[1] != '1' && r[1] != '2') || len(r) != 2 {
			ll.panic("malformed read index: %v", r)
		}
		filesByRIndex[r] = inputFile
	}
	// TODO adapt to include line in keys
	for rIndex in ["R1", "R2", "I1", "I2"] {
		inputFile := filesByRIndex[rIndex]
		if is_undefined(inputFile) {
			continue
		}
		fileName := "input_S1_L001_" + rIndex + "001." + fileExtension

//		ll.print("print fileName in loop", fileName)
		cellRanger.addFile(path.join("fastqs",fileName), inputFile)
	}

	cellRanger = cellRanger.
		arg("--sample=input_").
		arg("--create-bam=true").
		// arg("--nthreads=4").
		arg("--id=input_").
		saveFile("./outs/web_summary.html").
		saveFile("./outs/filtered_feature_bc_matrix/barcodes.tsv.gz").
		saveFile("./outs/filtered_feature_bc_matrix/features.tsv.gz").
		saveFile("./outs/filtered_feature_bc_matrix/matrix.mtx.gz").
		saveFile("./outs/filtered_feature_bc_matrix/possorted_genome_bam.bam").
		printErrStreamToStdout().
		saveStdoutContent().
		cache(24 * 60 * 60 * 1000).
		run()

	cellRangerLog := cellRanger.getStdoutStream()
	cellRangerReport := cellRanger.getFile("web_summary.html")
	bamFile := cellRanger.getFile("possorted_genome_bam.bam")
	cellRangerBarcodes := cellRanger.getFile("barcodes.tsv.gz")
	cellRangerFeatures := cellRanger.getFile("features.tsv.gz")
	cellRangerCounts := cellRanger.getFile("matrix.mtx.gz")

	countMatrixCsv := exec.builder().
		cmd("python").
		arg("generateCountsCsv.py").
		addFile("barcodes.tsv.gz", cellRangerBarcodes).
		addFile("features.tsv.gz", cellRangerFeatures).
		addFile("matrix.mtx.gz", cellRangerCounts).
		arg("--matrix").arg("matrix.mtx.gz").
		arg("--barcodes").arg("barcodes.tsv.gz").
		arg("--features").arg("features.tsv.gz").
		arg("--output").arg("countMatrix.csv").
		printErrStreamToStdout().
		saveStdoutContent().
		cache(24 * 60 * 60 * 1000).
		run()

	countCsvParams := pfCountsConv.getColumns(blockId, species)

	countMatrixImport := xsv.importFile(countMatrixCsv.getFile("countMatrix.csv"), "csv", countCsvParams, { dataOnly: true })

	// PFrame
	//   rawCounts.data
	//   rawCounts.spec
	// after we add { dataOnly: true }
	//   rawCounts [ containing value from rawCounts.data ]

	output := {
		cellRangerLog: cellRangerLog,
		cellRangerReport: file.exportFile(cellRangerReport),
		alignedBAM: bamFile,
		countMatrixImport: countMatrixImport
	}

	return output

})
