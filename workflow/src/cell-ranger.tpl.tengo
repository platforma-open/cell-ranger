self := import("@platforma-sdk/workflow-tengo:tpl")

json := import("json")
path := import("@platforma-sdk/workflow-tengo:path")

ll := import("@platforma-sdk/workflow-tengo:ll")
exec := import("@platforma-sdk/workflow-tengo:exec")
file := import("@platforma-sdk/workflow-tengo:file")
assets := import("@platforma-sdk/workflow-tengo:assets")
maps := import("@platforma-sdk/workflow-tengo:maps")
pConstants := import("@platforma-sdk/workflow-tengo:pframes.constants")

// Import product key
productKey := import(":libs.product-key")

genomes := import(":libs.genomes")

self.defineOutputs(
	"cellRangerLog",
	"cellRangerReport",
	"summaryContent",
	"rawCountsCsv",
	"normCountsCsv",
	"cellMetricsCsv"
)

self.body(func(inputs) {
	inputData := inputs[pConstants.VALUE_FIELD_NAME]
	species := inputs.species
	fileExtension := inputs.fileExtension
	blockId := inputs.blockId

	mem := !is_undefined(inputs.mem) ? string(inputs.mem) + "GiB" : "64GB"
	cpu := !is_undefined(inputs.cpu) ? inputs.cpu : 16

	genomeAssets := genomes.getGenomeAsset(species)

	inputDataMeta := inputData.getDataAsJson()

	cellRanger := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.software-cellranger:cellranger")).
		mem(mem).
		cpu(cpu).
		// addAsset(genomeAssets, "genomeIndex").
		addAsset(genomeAssets, "genomeIndex", "fasta/").
		addAsset(genomeAssets, "genomeIndex", "genes/").
		addAsset(genomeAssets, "genomeIndex", "star/").
		addAsset(genomeAssets, "genomeIndex", "reference.json").
		arg("count").
		arg("--transcriptome=genomeIndex").
		arg("--fastqs=fastqs")
		// enableMnz(productKey)

	inputMap := inputData.inputs()

	if inputDataMeta.keyLength == 1 {
		for sKey in maps.getKeys(inputMap) {
			inputFile := inputMap[sKey]
			key := json.decode(sKey)
			if len(key) != 1 {
				ll.panic("malformed key: %v", sKey)
			}
			r := key[0]
			if (r[0] != 'R' && r[0] != "I") || (r[1] != '1' && r[1] != '2') || len(r) != 2 {
				ll.panic("malformed read index: %v", r)
			}
			fileName := "input_S1_L001" + "_" + r + "_001." + fileExtension
			cellRanger.addFile(path.join("fastqs",fileName), inputFile)
			// , {
			// 	mnz: {
			// 		arg: "main_input",
			// 		metrics: ["sha256", "size"] // track usage
			// 	}
			// }
			// )
		}
	} else if inputDataMeta.keyLength == 2 {
		for sKey in maps.getKeys(inputMap) {
			inputFile := inputMap[sKey]
			key := json.decode(sKey)
			if len(key) != 2 {
				ll.panic("malformed key: %v", sKey)
			}
			lane := key[0]
			r := key[1]
			if (r[0] != 'R' && r[0] != "I") || (r[1] != '1' && r[1] != '2') || len(r) != 2 {
				ll.panic("malformed read index: %v", r)
			}
			if is_undefined(int(lane)) {
				ll.panic("malformed lane: %v", lane)
			}
			fileName := "input_S1_L" + lane + "_" + r + "_001." + fileExtension
			cellRanger.addFile(path.join("fastqs",fileName), inputFile)
			// , {
			// 	mnz: {
			// 		arg: "main_input",
			// 		metrics: ["sha256", "size"] // track usage
			// 	}
			// }
			// )
		}
	}

	cellRanger = cellRanger.
		// arg("--sample=input_").
		arg("--create-bam=false").
		arg("--id=sample").
		argWithVar("--localcores={system.cpu}").
		argWithVar("--localmem={system.ram.gb}").
		saveFile("sample/outs/web_summary.html").
		saveFile("sample/outs/filtered_feature_bc_matrix/barcodes.tsv.gz").
		saveFile("sample/outs/filtered_feature_bc_matrix/features.tsv.gz").
		saveFile("sample/outs/filtered_feature_bc_matrix/matrix.mtx.gz").
		// saveFile("sample/outs/possorted_genome_bam.bam").
		saveFileContent("sample/outs/metrics_summary.csv").
		printErrStreamToStdout().
		run()

	cellRangerLog := cellRanger.getStdoutStream()
	cellRangerReport := cellRanger.getFile("sample/outs/web_summary.html")
	// bamFile := cellRanger.getFile("possorted_genome_bam.bam")
	cellRangerBarcodes := cellRanger.getFile("sample/outs/filtered_feature_bc_matrix/barcodes.tsv.gz")
	cellRangerFeatures := cellRanger.getFile("sample/outs/filtered_feature_bc_matrix/features.tsv.gz")
	cellRangerCounts := cellRanger.getFile("sample/outs/filtered_feature_bc_matrix/matrix.mtx.gz")

	countMatrixCsv := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.cellranger.software:counts-csv")).
		mem(mem).
		cpu(cpu).
		addFile("barcodes.tsv.gz", cellRangerBarcodes).
		addFile("features.tsv.gz", cellRangerFeatures).
		addFile("matrix.mtx.gz", cellRangerCounts).
		arg("--matrix").arg("matrix.mtx.gz").
		arg("--barcodes").arg("barcodes.tsv.gz").
		arg("--features").arg("features.tsv.gz").
		arg("--output").arg("rawCounts.csv").
		saveFile("rawCounts.csv").
		saveFile("rawCounts_normalized.csv").
		printErrStreamToStdout().
		run()

	rawCounts := countMatrixCsv.getFile("rawCounts.csv")
	normCounts := countMatrixCsv.getFile("rawCounts_normalized.csv")

	// Set default memory and CPU for imports
    defaultConvMem := "16GiB" // @TODO: set based on the size of the input
    defaultConvCpu := 1 // @TODO: set based on the size of the input

	cellMetricsCsv := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.cellranger.software:cell-metrics")).
		mem(mem).
		cpu(cpu).
		addFile("barcodes.tsv.gz", cellRangerBarcodes).
		addFile("features.tsv.gz", cellRangerFeatures).
		addFile("matrix.mtx.gz", cellRangerCounts).
		arg("--matrix").arg("matrix.mtx.gz").
		arg("--barcodes").arg("barcodes.tsv.gz").
		arg("--features").arg("features.tsv.gz").
		arg("--species").arg(species).
		arg("--output").arg(".").
		saveFile("cell_metrics.csv").
		printErrStreamToStdout().
		run()

	filteredCellsCountsCsv := exec.builder().
		software(assets.importSoftware("@platforma-open/milaboratories.cellranger.software:filter-cells")).
		mem(mem).
		cpu(cpu).
		addFile("rawCounts.csv", rawCounts).
		addFile("rawCounts_normalized.csv", normCounts).
		addFile("cell_metrics.csv", cellMetricsCsv.getFile("cell_metrics.csv")).
		arg("--raw_counts").arg("rawCounts.csv").
		arg("--normalized_counts").arg("rawCounts_normalized.csv").
		arg("--metrics").arg("cell_metrics.csv").
		arg("--output_raw").arg("filtered_rawCounts.csv").
		arg("--output_normalized").arg("filtered_rawCounts_normalized.csv").
		saveFile("filtered_rawCounts.csv").
		saveFile("filtered_rawCounts_normalized.csv").
		printErrStreamToStdout().
		run()

	output := {
		cellRangerLog: cellRangerLog,
		cellRangerReport: file.exportFile(cellRangerReport),
		summaryContent: cellRanger.getFileContent("sample/outs/metrics_summary.csv"),
		rawCountsCsv: filteredCellsCountsCsv.getFile("filtered_rawCounts.csv"),
		normCountsCsv: filteredCellsCountsCsv.getFile("filtered_rawCounts_normalized.csv"),
		cellMetricsCsv: cellMetricsCsv.getFile("cell_metrics.csv")
	}

	return output

})
